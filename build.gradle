group 'io.tsdb.opentsdb.discoveryplugins'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

configurations{
    providedCompile
    compile.extendsFrom providedCompile
}

dependencies {
    compile group: 'com.stumbleupon', name: 'async', version: '1.4.1'
    compile 'com.google.collections:google-collections:1.0'
    compile 'ch.qos.logback:logback-classic:1.1.6'
    compile 'org.kohsuke.metainf-services:metainf-services:1.7'
    compile group: 'io.netty', name: 'netty', version: '3.9.4.Final'
    compile group: 'org.jboss.errai.io.netty', name: 'netty-handler', version: '4.0.0.Alpha1.errai.r1'
    providedCompile files('libs/tsdb-2.3.0-RC1.jar')
    testCompile group: 'org.testng', name: 'testng', version:'6.8'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.10.19'
}

task fatJar(type: Jar, dependsOn: build) {
    baseName = project.name + '-all'
    manifest {
        attributes 'Main-Class': 'io.tsdb.opentsdb.ExecutePlugin'
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude {
                    it.path.contains('META-INF')
                }
            }
        }
    }
    with jar
}

jar {
    manifest {
        attributes 'Main-Class': 'io.tsdb.opentsdb.ExecutePlugin'
    }
}

task runJar(dependsOn:jar) << {
  javaexec { main="-jar"; args = [jar.archivePath, "--config", "src/main/resources/opentsdb.conf"]}
}

task runFatJar(dependsOn:jar) << {
  javaexec { main="-jar"; args = ['build/libs/' + project.name + '-all-' + project.version + '.jar', "--config", "src/main/resources/opentsdb.conf"]}
}
